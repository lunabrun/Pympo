!===============================================================================
! PROGRAM:          2PBeam_LinearLoad_Test
!
! PROJEKT:          REMODELING: Iterative adaptation of local material
!                   properties to local mechanical environment.
! TASK:             Simple problem to test linear distributed load, as it is
!					used in the example problem from Lian2010 paper.
!					Note: Beam modeled with 2D plain stress plate elements
! AUTHOR:           Bruno Luna
! LAST CHANGES:     13.03.2022
!
! COMPUTER:         MHH PC
! OPERATING SYSTEM: Windows
! ANSYS Version:    License: Mechanical Enterprise 2022 R1
!
! CALL:             /INPUT,2PBeam_LinearLoad_Test.inp
!
!===============================================================================
 
!=== Initialization
FINISH              ! Stops all modules (preprocessor, solution, postprocessor)
/CLEAR              ! Start a new analysis, delete the old database (file.db)
 
/PLOPTS,INFO,AUTO ! Good old contour labeling style
  
!===============================================================================
!  Parameters (lenght in mm, force in N => stiffness and stress in N/mm^2(MPa))
!===============================================================================
 
!--- Geometry parameters
Length   =     3.5        	! Length (x) of plate
Height   =     0.25        	! Height (y) of plate
Thickn   =     0.2        	! Thickness of plate
 
Meshrefine =     1	    	! Global mesh refinement level (1(fine) ... 10(rough))
 
!--- Load paramters
DistForce    =  7/Thickn    ! Load (Force, vertical) per mm (N/mm^2 in paper?wrong?)
 
!--- Initial material parameters
Young_ini =  210000.0       ! Initial Young's modulus of the plates material (steel)
Poiss_ini =       0.3       ! Initial Poisson's ratio of the plates material (steel)
 
!===============================================================================
! A. Preprocessor (Setting up the model)
!===============================================================================
/PREP7                       ! Switch to the preprocessor modul
  
!=== Build the geometry by means of bottom-up method
 
!--- Create plate
K,1,    0.0,    0.0, 0.0     ! Define keypoint via its coordinates x, y, z
K,2, Length,    0.0, 0.0     ! Define keypoint via its coordinates x, y, z
K,3, Length, Height, 0.0     ! Define keypoint via its coordinates x, y, z
K,4,    0.0, Height, 0.0     ! Define keypoint via its coordinates x, y, z
LSTR,1,2                     ! Connect keypoints 1 and 2 to create line 1
LSTR,2,3                     ! Connect keypoints to create a line
LSTR,3,4                     ! Connect keypoints to create a line
LSTR,4,1                     ! Connect keypoints to create a line
AL,ALL                       ! Create area by all selected lines (1, 2, 3, 4)

ALLSEL                       ! Select everything
APLOT                        ! Plot areas

!=== Meshing
ET,1,PLANE182                ! Define the local element type 1 as a PLANE182 element
KEYOPT,1,1,2                 ! Sets key option 1 (of elem type 1) to 2 (enhanc. strain formul.)
KEYOPT,1,3,3                 ! Sets key option 3 (of elem type 1) to 3 (plane stress w/ thickn)
!--- Define real constants
R,1,Thickn              	 ! Set thicknessess of the plate in "real set" no. 1

SMRTSIZE,Meshrefine          ! Global mesh refinement level (1(fine) ... 10(rough))
AMESH,ALL                    ! Meshing areas with this number
EPLOT                        ! Plot elements
*GET,nElem,ELEM,0,COUNT      ! Get number of elements

!===============================================================================
!=== Initial definition of material properties, one for each of the FE
ALLSEL
*DO,el,1,nElem               ! Loop over all elements
   MP,EX,el,Young_ini        ! Assign Young modulus
   MP,PRXY,el,Poiss_ini      ! Assign Poisson number
   EMODIF,el,MAT,el          ! Assign material type el to element el
*ENDDO

!=== Apply Load and Boundary Conditions
!--- Displacement boundary conditions
NSEL,S,LOC,Y,0.0             ! Select all nodes at y = 0 (bottom side)
NSEL,R,LOC,X,0.0			 ! From previous selected nodes, get bottom left corner (x=0,y=0)
D,ALL,UY,0.0                 ! Set displacement uy to zero for selected nodes
D,ALL,UX,0.0                 ! Set displacement ux to zero for selected node
ALLSEL                       ! Select all entities

NSEL,S,LOC,Y,0.0             ! Select all nodes at y = 0 (bottom side)
NSEL,R,LOC,X,Length			 ! From previous selected nodes, get bottom right corner (x=L,y=0)
D,ALL,UY,0.0                 ! Set displacement uy to zero for selected nodes
ALLSEL                       ! Select all entities

!--- Applying the load
SFGRAD,PRES,0,X,0.0,-DistForce/Length
NSEL,S,LOC,Y,Height
SF,ALL,PRES,DistForce
SFGRAD
ALLSEL

/SOLU
ANTYPE,0
!NLGEOM,ON
SOLVE

/POST1
SET,LAST
!PLESOL,S,EQV
PLNSOL,U,Y

:ENDSCRIPT