!===============================================================================
! PROGRAM:          Simple Bone-Tooth as Beam 
!                   (Based on MMSM 1 Lab No 9 (Ansys Exercise 7) - Uni Ulm)
! PROJEKT:          REMODELING: Iterative adaptation of local material
!                   properties to local mechanical environment.
! TASK:             2D model of a plate with a cantilever beam on top
!                   with initially constant material properties undergoes an
!                   iterative change of element material properties similar
!                   to bone remodeling or growth of wood.  The changing rate
!                   is modeled dependent on the local strain energy density.
! AUTHOR:           Bruno Luna
! LAST CHANGES:     07.03.2022
!
! COMPUTER:
! OPERATING SYSTEM:
! ANSYS Version:    License: Mechanical Enterprise 2020 R2
!
! CALL:             /INPUT,SimpleBoneTooth_Beam.inp
!
!===============================================================================
 
!=== Beginning
FINISH              ! Stops all modules (preprocessor, solution, postprocessor)
/CLEAR              ! Start a new analysis, delete the old database (file.db)
 
/PLOPTS,INFO,AUTO ! Good old contour labeling style
  
!===============================================================================
!  Parameters (lenght in mm, forces in N => stiffnesses and stresses in N/mm)
!===============================================================================
 
!--- Controlling parameters
nIter    =     10        	! Number of iterations for material transformation
 
!--- Geometry parameters
Length   =     500.0        ! Length (x) of plate
Height   =     500.0        ! Height (y) of plate
Thickn   =      20.0        ! Thickness of plate
Diameter =     400.0        ! Diameter of the hole
x_hole   =     250.0        ! Verticale position of the holes center (from left)
y_hole   =     250.0        ! Horizontale position of the holes center (from bottom)
 
Meshrefine =     1        	! Global mesh refinement level (1(fine) ... 10(rough))
 
!--- Load paramters
Force    =  -100000.0       ! Load (Force, vertical)
 
!--- Initial material parameters
Young_ini =  210000.0       ! Initial Young's modulus of the plates material (steel)
Poiss_ini =       0.3       ! Initial Poisson's ratio of the plates material (steel)
 
!--- Remodeling function parameters
SollWert  =  0.5            ! Setpoint for strain energy density
Epsilon   =  SollWert/10    ! Switch breadth
FaktorAbb =  1000000        ! Gradient of resorption function
FaktorAuf =  FaktorAbb      ! Gradient of grow function
Limit_U  =  SollWert - Epsilon    ! Lower switch limit
Limit_O  =  SollWert + Epsilon    ! Higher switch limit
EMod_min =  Young_ini/100  ! Minimal allowed Young Modulus (E-Modulus)
EMod_max =  Young_ini*2    ! Maximum allowed Young Modulus (E-Modulus)
 
!===============================================================================
! A. Preprocessor (Setting up the model)
!===============================================================================
/PREP7                       ! Switch to the preprocessor modul
  
!=== Build the geometry by means of bottom-up method
 
!--- Create plate
K,1,    0.0,    0.0, 0.0     ! Define keypoint via its coordinates x, y, z
K,2, Length,    0.0, 0.0     ! Define keypoint via its coordinates x, y, z
K,3, Length, Height, 0.0     ! Define keypoint via its coordinates x, y, z
K,4,    0.0, Height, 0.0     ! Define keypoint via its coordinates x, y, z
LSTR,1,2                     ! Connect keypoints 1 and 2 to create line 1
LSTR,2,3                     ! Connect keypoints to create a line
LSTR,3,4                     ! Connect keypoints to create a line
LSTR,4,1                     ! Connect keypoints to create a line
AL,ALL                       ! Create area by all selected lines (1, 2, 3, 4)
 
!--- Create circular area
LSEL,none                    ! Unselect all previous defined lines
K,5,x_hole,y_hole,0.0    	 ! Create keypoint at circle center
CIRCLE,5,Diameter/2          ! Create circle lines
AL,ALL                       ! Create area by all selected lines
ALLSEL                       ! Select everything
 
!--- Cut hole (boolean operation)
ASBA,1,2,,DELETE,DELETE      ! Cut out hole (2) from plate (1) and get plate with hole (3)
/PNUM,area,1                 ! Switch numbering of lines on
APLOT                        ! Plot areas

!=== Meshing
ET,1,PLANE182                ! Define the local element type 1 as a PLANE182 element
KEYOPT,1,3,3                 ! Sets key option 3 (of elem type 1) to 3 (plane stress with thickn)
SMRTSIZE,Meshrefine          ! Global mesh refinement level (1(fine) ... 10(rough))
AMESH,ALL                    ! Meshing areas with this number
EPLOT                        ! Plot elements
*GET,nElem,ELEM,0,COUNT      ! Get number of elements

!===============================================================================
!=== Initial definition of material properties, one for each of the FE
ALLSEL
*DO,el,1,nElem               ! Loop over all elements
   MP,EX,el,Young_ini        ! Assign Young modulus
   MP,PRXY,el,Poiss_ini      ! Assign Poisson number
   EMODIF,el,MAT,el          ! Assign material type el to element el
*ENDDO
 
!=== Define real constants
R,1,Thickn                   ! Define thickness of the plate into real set number 1

!=== Apply Load and Boundary Conditions
!--- Displacement boundary conditions
NSEL,S,LOC,X,0.0             ! Select all nodes at x = 0 (left side)
D,ALL,UX,0.0                 ! Set displacement ux to zero for selected nodes
D,ALL,UY,0.0                 ! Set displacement uy to zero for selected nodes
ALLSEL                       ! Select all entities
!--- Applying the load as a single force
nn = node(Length,Height,0.0) ! Get node number at location x, y, z (bottom right corner)
F,nn,FY,Force                ! Apply load at node with number nn
 
!===============================================================================
!=== Initialize arrays for element-wise data
*DIM,AR_IstWert,array,nElem  ! Vector for current status
*DIM,AR_EMod,array,nElem     ! Vector for E-Modulus
*DIM,AR_DeltaE,array,nElem   ! Vector for delta E-Modulus
 
!===============================================================================
! B. START REMODELING LOOP
!===============================================================================
*DO,iter,1,nIter            ! Loop over transformation steps
   !============================================================================
   !====== Solution
   /SOLU                    ! Switch to the solution module
   ANTYPE,0                 ! Select the static analysis type
   NLGEOM,ON			    ! Switch large geometry deformation on  
   SOLVE                    ! Solve current load step
   !============================================================================
   !====== Postprocessor
   /POST1                   ! Switch to the postprocessor module
   SET, LAST                ! Get results from last step
   !============================================================================
   !=== Read SED (Strain Energy Density)
   ETABLE,ET_SED,SEND,ELASTIC         		! Save element-wise results in etable
   *DO,el,1,nElem                     		! Loop over all elements
      *GET,AR_IstWert(el),ELEM,el,ETAB,ET_SED   ! Read ele.-wise result to vector
   *ENDDO
 
   !============================================================================
   !=== Output intermediate result
   /title,'Strain-Energy-Density (SED) Step = %iter%'
   /dscale,1,1.0
   /contour,,,0,,2*Limit_O
   PLESOL,SEND,ELASTIC
   
   !============================================================================
   !=== Pause einlegen und evtl. abbrechen
   !*ask,aa,'= Continue! 0 = Stop',1
   !*IF,aa,eq,0,:ENDE
 
   !============================================================================
   !=== Prepare remodeling
   *DO,el,1,nElem            ! Loop over all elements
      *IF,AR_IstWert(el),LT,Limit_U,THEN
         AR_DeltaE(el) = FaktorAbb*(AR_IstWert(el) - Limit_U)
      *ELSEIF,AR_IstWert(el),GT,Limit_O,THEN
         AR_DeltaE(el) = FaktorAuf*(AR_IstWert(el) - Limit_O)
      *ELSE
         AR_DeltaE(el) = 0.0
      *ENDIF
   *ENDDO
 
   !============================================================================
   !=== Remodeling, i.e., change material
   /PREP7                				! Switch to preprocessor
   *DO,el,1,nElem            			! Loop over all elements
      *GET,mat_no,MAT,el        		! Get material number of each element
      *GET,AR_EMod(el),EX,mat_no        ! Get E-modulus of that material number
      AR_EMod(el) = AR_EMod(el) + AR_DeltaE(el) ! Change E-Modulus
      *IF,AR_EMod(el),LE,EMod_min,THEN    	! Limit reduction
         AR_EMod(el) = EMod_min
      *ELSEIF,AR_EMod(el),GE,EMod_max,THEN 	! Limit grow
         AR_EMod(el) = EMod_max
      *ENDIF
      MP,EX,el,AR_EMod(el)                 	! E-Modul zuweisen
   *ENDDO

*ENDDO 
!===============================================================================
! END OF LOOP
!===============================================================================

!===============================================================================
! C. POSTPROCESSOR
!===============================================================================
/POST1                    ! Switch to postprocessor

!=============================================================================== 
!=== Plot of E-Modulus
!--- Create element table for visualization of E-Modulus; 
!--- Epto,x as placeholder at first, will be overwritten
ETABLE,ET_emod,EPTO,X

!--- Fill table with E-Modulus values
*VPUT,AR_EMod(1),ELEM,1,ETAB,ET_emod,0

!--- Plot
/SHOW,PNG
/CONTOUR,,,EMod_min,,EMod_max
/TITLE,'E-Modulus distribution after step  = %iter%'
PLETAB, ET_emod, NOAVG

!===============================================================================  
!=== Pause einlegen und evtl. abbrechen
!*ASK,aa,'= Continue! 0 = Stop',1
!*IF,aa,EQ,0,:ENDSCRIPT
 
!=============================================================================== 
!=== SED
/SHOW,PNG
/TITLE,'Strain-Energy-Density (SED) Step = %iter%'
/DSCALE,1,1.0
/CONTOUR,,,0,,2*Limit_O
PLESOL, SEND, ELASTIC
 
!===============================================================================
! END LABEL
!===============================================================================
:ENDSCRIPT